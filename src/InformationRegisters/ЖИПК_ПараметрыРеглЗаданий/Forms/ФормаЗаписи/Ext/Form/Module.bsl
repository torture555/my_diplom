
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Расписание = Новый РасписаниеРегламентногоЗадания;
		УстановитьОписаниеРасписания();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Расписание = ТекущийОбъект.Расписание.Получить();
	
	Задание = РегламентныеЗаданияСервер.Задание(ТекущийОбъект.Идентификатор);
	Если Задание <> Неопределено И Строка(Задание.Расписание) <> Строка(Расписание) Тогда
		Расписание = Задание.Расписание;
		Сообщить("Расписание было изменено через обработку ""Регламентые и фоновые задания"". Расписание обновлено.");
		Модифицированность = Истина;
	КонецЕсли;
	
	УстановитьОписаниеРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Расписание = Новый ХранилищеЗначения(Расписание, Новый СжатиеДанных(9));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользованиеПриИзменении(Элемент)
	
	УстановитьОписаниеРасписания();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьРасписание(Команда)
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Диалог.Показать(Новый ОписаниеОповещения("ОткрытьРасписаниеЗавершение", ЭтотОбъект) );
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеЗавершение(НовоеРасписание, Контекст) Экспорт

	Если НовоеРасписание <> Неопределено Тогда
		Расписание = НовоеРасписание;
		УстановитьОписаниеРасписания();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЭтоНовый()
	
	Возврат Не ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи);
	
КонецФункции

&НаСервере
Процедура УстановитьОписаниеРасписания()
	
	ОписаниеРасписания = ?(Расписание = Неопределено ИЛИ Не Запись.Использование, 
		"<не задано>", Строка(Расписание) );
	
КонецПроцедуры

#КонецОбласти
